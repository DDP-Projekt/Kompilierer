Binde "Duden/Zeichen" ein.
Binde "Duden/Listen" ein.
Binde "Duden/TextIterator" ein.

[
	Gibt "" zurück.
	Nutzen: Der Text t ist ein leerer Text.
]
Die öffentliche Funktion Leerer_Text gibt einen Text zurück, macht:
	Gib "" zurück.
Und kann so benutzt werden:
	"ein leerer Text" oder
	"einen leeren Text"

[
	Gibt den ersten Buchstaben von dem gegebenen Text zurück.
]
Die öffentliche Funktion Erster_Buchstabe mit dem Parameter t vom Typ Text, gibt einen Buchstaben zurück, macht:
	Gib t an der Stelle 1 zurück.
Und kann so benutzt werden:
	"der erste Buchstabe von <t>",
	"den ersten Buchstaben von <t>",
	"dem ersten Buchstaben von <t>"

[
	Gibt den enten Buchstaben von dem gegebenen Text zurück.
]
Die öffentliche Funktion Nter_Buchstabe mit den Parametern n und t vom Typ Zahl und Text, gibt einen Buchstaben zurück, macht:
	Gib t an der Stelle n zurück.
Und kann so benutzt werden:
	"der <n> Buchstabe von <t>",
	"den <n> Buchstaben von <t>",
	"dem <n> Buchstaben von <t>",
	"der <n>. Buchstabe von <t>",
	"den <n>. Buchstaben von <t>",
	"dem <n>. Buchstaben von <t>"

[
	Gibt den letzten Buchstaben von dem gegebenen Text zurück.
]
Die öffentliche Funktion Letzter_Buchstabe mit dem Parameter t vom Typ Text, gibt einen Buchstabe zurück, macht:
	Gib t an der Stelle (die Länge von t) zurück.
Und kann so benutzt werden:
	"der letzte Buchstabe von <t>",
	"den letzten Buchstaben von <t>",
	"dem letzten Buchstaben von <t>"

[
	Entfernt eine feste Anzahl von Buchstaben vom Anfang eines gegebenen Textes.
	Ist die Länge des Textes kürzer als die zu löschende Anzahl, gibt die Funktion einen leeren Text zurück.
	Werte für Anzahl kleiner als 0 wird wie 0 gewertet.
	z.B.: 
		Der Text t ist "abcdef".
		Entferne 3 Buchstaben am Anfang von t.
		t: "def"

		Der Text t ist "abc".
		Entferne 5 Buchstaben am Anfang von t.
		t: ""
]
Die öffentliche Funktion Entferne_Anzahl_Vorne_Mutierend mit den Parametern text und anzahl vom Typ Text Referenz und Zahl, gibt nichts zurück, macht:
    Wenn die Länge von text kleiner als, oder anzahl ist, dann:
        Speichere "" in text.
    Sonst speichere text ab dem (anzahl plus 1). Element in text.
Und kann so benutzt werden:
    "Entferne <anzahl> Buchstaben am Anfang von <text>"

[
	Entfernt eine feste Anzahl von Buchstaben vom Ende eines gegebenen Textes.
	Ist die Länge des Textes kürzer als die zu löschende Anzahl, gibt die Funktion einen leeren Text zurück.
	Werte für Anzahl kleiner als 0 wird wie 0 gewertet.
	z.B.: 
		Der Text t ist "abcdef".
		Entferne 3 Buchstaben am Ende von t.
		t: "abc"

		Der Text t ist "abc".
		Entferne 5 Buchstaben am Ende von t.
		t: ""
]
Die öffentliche Funktion Entferne_Anzahl_Hinten_Mutierend mit den Parametern text und anzahl vom Typ Text Referenz und Zahl, gibt nichts zurück, macht:
    Wenn die Länge von text kleiner als, oder anzahl ist, dann:
        Speichere "" in text.
    Sonst speichere text bis zum (die Länge von text minus anzahl). Element in text.
Und kann so benutzt werden:
    "Entferne <anzahl> Buchstaben am Ende von <text>"

[
	Entfernt eine feste Anzahl von Buchstaben vom Anfang eines gegebenen Textes.
	Ist die Länge des Textes kürzer als die zu löschende Anzahl, gibt die Funktion einen leeren Text zurück.
	Werte für Anzahl kleiner als 0 wird wie 0 gewertet.
	z.B.: 
		Der Text t ist "abcdef".
		Entferne 3 Buchstaben am Anfang von t.
		t: "def"

		Der Text t ist "abc".
		Entferne 5 Buchstaben am Anfang von t.
		t: ""
]
Die öffentliche Funktion Entferne_Anzahl_Vorne mit den Parametern text und anzahl vom Typ Text und Zahl, gibt einen Text zurück, macht:
    Wenn die Länge von text kleiner als, oder anzahl ist, dann:
        Gib "" zurück.
    Gib text ab dem (anzahl plus 1). Element zurück.
Und kann so benutzt werden:
    "<text> mit den ersten <anzahl> Buchstaben entfernt"

[
	Entfernt eine feste Anzahl von Buchstaben vom Ende eines gegebenen Textes.
	Ist die Länge des Textes kürzer als die zu löschende Anzahl, gibt die Funktion einen leeren Text zurück.
	Werte für Anzahl kleiner als 0 wird wie 0 gewertet.
	z.B.: 
		Der Text t ist "abcdef".
		Entferne 3 Buchstaben am Ende von t.
		t: "abc"

		Der Text t ist "abc".
		Entferne 5 Buchstaben am Ende von t.
		t: ""
]
Die öffentliche Funktion Entferne_Anzahl_Hinten mit den Parametern text und anzahl vom Typ Text und Zahl, gibt einen Text zurück, macht:
    Wenn die Länge von text kleiner als, oder anzahl ist, dann:
        Gib "" zurück.
    Gib text bis zum (die Länge von text minus anzahl). Element zurück.
Und kann so benutzt werden:
    "<text> mit den letzten <anzahl> Buchstaben entfernt"

[
	Entfernt alle gegebenen Buchstaben vom Anfang eines gegebenen Textes.
	z.B.: 
		Der Text t ist "aaaaaaahallo"
		Entferne alle 'a' vor t.
		t: "hallo"
]
Die öffentliche Funktion Trim_Anfang mit den Parametern text und zeichen vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Wenn die Länge von text gleich 0 ist, verlasse die Funktion.
	Die Zahl index ist 1.
	Solange (text an der Stelle index) gleich zeichen ist, mache:
		Erhöhe index um 1.
		Wenn index größer als die Länge von text ist, dann:
			Speichere "" in text.
			Verlasse die Funktion.
	Speichere text ab dem index. Element in text.
Und kann so benutzt werden:
	"Entferne alle <zeichen> vor <text>"

[
	Siehe TrimAnfang
]
Die öffentliche Funktion Trim_Anfang_Wert mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt einen Text zurück, macht:
	Entferne alle zeichen vor text.
	Gib text zurück.
Und kann so benutzt werden:
	"<text> mit allen <zeichen> davor entfernt"

[
	Entfernt alle gegebenen Buchstaben vom Ende eines gegebenen Textes.
	z.B.: 
		Der Text t ist "hallo!!!!!!!!!!"
		Entferne alle '!' nach t.
		t: "hallo"
]
Die öffentliche Funktion Trim_Ende mit den Parametern text und zeichen vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Wenn die Länge von text gleich 0 ist, verlasse die Funktion.

	Die Zahl index ist die Länge von text.
	Wenn index gleich 0 ist, verlasse die Funktion.
	Solange (text an der Stelle index) gleich zeichen ist, mache:
		Verringere index um 1.
		Wenn index kleiner als 1 ist, dann:
			Speichere "" in text.
			Verlasse die Funktion.

	Speichere text bis zum index. Element in text.
Und kann so benutzt werden:
	"Entferne alle <zeichen> nach <text>"

[
	Siehe TrimEnde
]
Die öffentliche Funktion Trim_Ende_Wert mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt einen Text zurück, macht:
	Entferne alle zeichen nach text.
	Gib text zurück.
Und kann so benutzt werden:
	"<text> mit allen <zeichen> danach entfernt"

[
	Entfernt alle gegebenen Buchstaben vom Anfang und Ende eines gegebenen Textes.
	z.B.: 
		Der Text t ist "!!!!!hallo!!!!!!!!!!"
		Entferne alle '!' vor und nach t.
		t: "hallo"
]
Die öffentliche Funktion Trim mit den Parametern text und zeichen vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Wenn die Länge von text gleich 0 ist, verlasse die Funktion.
	
	Die Zahl startIndex ist 1.
	Die Zahl stopIndex ist die Länge von text.
	Solange (text an der Stelle startIndex) gleich zeichen ist und startIndex kleiner als die Länge von text ist, erhöhe startIndex um 1.
	Solange (text an der Stelle stopIndex) gleich zeichen ist und stopIndex ungleich 1 ist, verringere stopIndex um 1.
	Wenn startIndex gleich die Länge von text ist und stopIndex gleich 1 ist, dann:
	  	Speichere "" in text.
		Verlasse die Funktion.
	
	Speichere text im Bereich von startIndex bis stopIndex in text.
Und kann so benutzt werden:
	"Entferne alle <zeichen> vor und nach <text>"

[
	Siehe Trim
]
Die öffentliche Funktion Trim_Wert mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt einen Text zurück, macht:
	Entferne alle zeichen vor und nach text.
	Gib text zurück.
Und kann so benutzt werden:
	"<text> mit allen <zeichen> davor und danach entfernt"

[
	Gibt zurück ob der gegebenen Text den gegebenen Buchstaben enthält. 
]
Die öffentliche Funktion Text_Enthält_Buchstabe mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt einen Wahrheitswert zurück, macht:
	Für jeden Buchstaben b in text, wenn b gleich zeichen ist, gib wahr zurück.
	Gib falsch zurück.
Und kann so benutzt werden:
	"<text> <zeichen> <!nicht> enthält"

[
	Gibt zurück wie oft der gegebenen Text den gegebenen Buchstaben enthält. 
]
Die öffentliche Funktion Text_Anzahl_Buchstabe mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt eine Zahl zurück, macht:
	Die Zahl anz ist 0.
	Für jeden Buchstaben b in text, wenn b gleich zeichen ist, erhöhe anz um 1.
	Gib anz zurück.
Und kann so benutzt werden:
	"die Anzahl der <zeichen> Buchstaben in <text>"

[
	Gibt zurück ob der gegebene Text (text) den Subtext (suchText) enthält.
]
Die öffentliche Funktion Text_Enthält_Text mit den Parametern text und suchText vom Typ Text und Text, gibt einen Wahrheitswert zurück, macht:
	Wenn text gleich suchText ist, gib wahr zurück.
	Die Zahl nt ist die Länge von text.
	Die Zahl ns ist die Länge von suchText.
	Wenn nt gleich 0 ist oder ns gleich 0 ist, gib falsch zurück.

	Die Zahl startIndex ist 1.
	Die Zahl endIndex ist ns.

	Solange endIndex kleiner als, oder nt ist, mache:
		Der Text subtext ist text im Bereich von startIndex bis endIndex.
		Wenn subtext gleich suchText ist, gib wahr zurück.
		
		Speichere startIndex plus ns in endIndex.
		Erhöhe startIndex um 1.
	Gib falsch zurück.
Und kann so benutzt werden:
	"<text> <suchText> <!nicht> enthält"

[
	Gibt zurück wie oft der gegebene Text (text) den Subtext (suchText) enthält.
]
Die öffentliche Funktion Text_Anzahl_Text mit den Parametern text und suchText vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Die Zahl nt ist die Länge von text.
	Die Zahl ns ist die Länge von suchText.
	Wenn nt gleich 0 ist, gib 0 zurück.
	Wenn ns gleich 0 ist, gib nt zurück.

	Die Zahl startIndex ist 1.
	Die Zahl endIndex ist ns.
	Die Zahl anz ist 0.

	Solange endIndex kleiner als, oder nt ist, mache:
		Der Text subtext ist text im Bereich von startIndex bis endIndex.
		Wenn subtext gleich suchText ist, erhöhe anz um 1.
		
		Speichere startIndex plus ns in endIndex.
		Erhöhe startIndex um 1.
	Gib anz zurück.
Und kann so benutzt werden:
	"die Anzahl der Subtexte <suchText> in <text>"

[
	Gibt zurück wie oft der gegebene Text (text) den Subtext (suchText) nicht überlappend enthält.
]
Die öffentliche Funktion Text_Anzahl_Text_Nicht_Überlappend mit den Parametern text und suchText vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Die Zahl nt ist die Länge von text.
	Die Zahl ns ist die Länge von suchText.
	Wenn nt gleich 0 ist, gib 0 zurück.
	Wenn ns gleich 0 ist, gib nt zurück.

	Die Zahl startIndex ist 1.
	Die Zahl endIndex ist ns.
	Die Zahl anz ist 0.

	Solange endIndex kleiner als, oder nt ist, mache:
		Der Text subtext ist text im Bereich von startIndex bis endIndex.
		Wenn subtext gleich suchText ist, erhöhe anz um 1.
		
		Erhöhe startIndex um ns.
		Erhöhe endIndex um ns.
	Gib anz zurück.
Und kann so benutzt werden:
	"die Anzahl der nicht überlappenden Subtexte <suchText> in <text>"

[
	Gibt zurück ob der gegebene Text mit dem gegebenen Buchstaben anfängt.
]
Die öffentliche Funktion Beginnt_Mit_Buchstabe mit den Parametern text und buchstabe vom Typ Text und Buchstabe, gibt einen Wahrheitswert zurück, macht:
	Wenn die Länge von text gleich 0 ist, gib falsch zurück.
	Gib wahr, wenn (text an der Stelle 1) gleich buchstabe ist, zurück.
Und kann so benutzt werden:
	"<buchstabe> <!nicht> am Anfang von <text> steht"

[
	Gibt zurück ob der gegebene Text mit dem gegebenen Text (suchText) anfängt.
]
Die öffentliche Funktion Beginnt_Mit_Text mit den Parametern text und suchText vom Typ Text und Text, gibt einen Wahrheitswert zurück, macht:
	Wenn die Länge von text gleich 0 ist oder die Länge von suchText gleich 0 ist, gib falsch zurück.
	Gib (text bis zum (die Länge von suchText). Element) gleich suchText ist zurück.
Und kann so benutzt werden:
	"<suchText> <!nicht> am Anfang von <text> steht"

[
	Gibt zurück ob der gegebene Text mit dem gegebenen Buchstaben endet.
]
Die öffentliche Funktion Endet_Mit_Buchstabe mit den Parametern text und buchstabe vom Typ Text und Buchstabe, gibt einen Wahrheitswert zurück, macht:
	Wenn die Länge von text gleich 0 ist, gib falsch zurück.
	Gib (text an der Stelle (die Länge von text)) gleich buchstabe ist zurück.
Und kann so benutzt werden:
	"<buchstabe> <!nicht> am Ende von <text> steht"

[
	Gibt zurück ob der gegebene Text mit dem gegebenen Text (suchText) endet.
]
Die öffentliche Funktion Endet_Mit_Text mit den Parametern text und suchText vom Typ Text und Text, gibt einen Wahrheitswert zurück, macht:
	Wenn die Länge von text gleich 0 ist oder die Länge von suchText gleich 0 ist, gib falsch zurück.
	Gib (text ab dem (die Länge von text minus die Länge von suchText plus 1). Element) gleich suchText ist zurück.
Und kann so benutzt werden:
	"<suchText> <!nicht> am Ende von <text> steht"

[
	Speichert einen leeren Text in text.
]
Die öffentliche Funktion Text_Leeren mit dem Parameter text vom Typ Text Referenz, gibt nichts zurück, macht:
	Speichere "" in text.
Und kann so benutzt werden:
	"Leere <text>"

[
	Fügt zwei Texte aneinander.
	f("ha", "lo") -> "halo"
]
Die öffentliche Funktion Text_An_Text_Fügen mit den Parametern text und elm vom Typ Text Referenz und Text, gibt nichts zurück, macht:
	Speichere text verkettet mit elm in text.
Und kann so benutzt werden:
	"Füge <elm> an <text> an"

[
	Fügt einen Buchstaben an einen Text.
	f("may", 'o') -> "mayo"
]
Die öffentliche Funktion Buchstabe_An_Text_Fügen mit den Parametern text und elm vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Speichere text verkettet mit elm in text.
Und kann so benutzt werden:
	"Füge <elm> an <text> an"

[
	Fügt einen Text in einen anderen an dem gegebenen Index ein.
]
Die öffentliche Funktion Text_In_Text_Einfügen mit den Parametern text, index und elm vom Typ Text Referenz, Zahl und Text, gibt nichts zurück, macht:
	Speichere text bis zum (index minus 1). Element verkettet mit elm verkettet mit text ab dem index. Element in text.
Und kann so benutzt werden:
	"Setze <elm> an die Stelle <index> von <text>"

[
	Fügt einen Buchstaben in einen Text an dem gegebenen Index ein.
]
Die öffentliche Funktion Buchstabe_In_Text_Einfügen mit den Parametern text, index und elm vom Typ Text Referenz, Zahl und Buchstabe, gibt nichts zurück, macht:
	Speichere text bis zum (index minus 1). Element verkettet mit elm verkettet mit text ab dem index. Element in text.
Und kann so benutzt werden:
	"Setze <elm> an die Stelle <index> von <text>"

[
	Fügt einen Text am Anfang eines Textes ein.
	f("hallo", " welt!") -> "hallo welt!"
]
Die öffentliche Funktion Text_Vor_Text_Stellen mit den Parametern text und elm vom Typ Text Referenz und Text, gibt nichts zurück, macht:
	Speichere elm verkettet mit text in text.
Und kann so benutzt werden:
	"Stelle <elm> vor <text>"

[
	Fügt einen Buchstaben am Anfang eines Textes ein.
	f("allo", 'h') -> "hallo"
]
Die öffentliche Funktion Buchstabe_Vor_Text_Stellen mit den Parametern text und elm vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Speichere elm verkettet mit text in text.
Und kann so benutzt werden:
	"Stelle <elm> vor <text>"

[
	Entfernt den Buchstaben an der Stelle index vom Text
]
Die öffentliche Funktion Lösche_Text mit den Parametern text und index vom Typ Text Referenz und Zahl, gibt nichts zurück, macht:
	Wenn die Länge von text gleich 0 ist, verlasse die Funktion.

	Wenn index gleich 1 ist und die Länge von text größer als 1 ist, dann:
		Speichere "" in text.
	Wenn aber index gleich 1 ist, dann:
		Speichere text ab dem 2. Element in text.
	Wenn aber index gleich die Länge von text ist, dann:
		Speichere text bis zum (die Länge von text minus 1). Element in text.
	Sonst:
		Speichere text bis zum (index minus 1). Element verkettet mit text ab dem (index plus 1). Element in text.
Und kann so benutzt werden:
	"Lösche das Element an der Stelle <index> aus <text>"

[
	Entfernt einen Bereich vom Text
]
Die öffentliche Funktion Lösche_Text_Bereich mit den Parametern text, start und end vom Typ Text Referenz, Zahl und Zahl, gibt nichts zurück, macht:
	Wenn start gleich 1 ist, dann:
		Speichere text ab dem (end plus 1). Element in text.
	Sonst:
		Speichere text bis zum (start minus 1). Element verkettet mit text ab dem (end plus 1). Element in text.
Und kann so benutzt werden:
	"Lösche alle Elemente im Bereich von <start> bis <end> aus <text>"

[
	Füllt den Text mit dem gegebenen Buchstaben
]
Die öffentliche Funktion Fülle_Text mit den Parametern text und elm vom Typ Text Referenz und Buchstabe, gibt nichts zurück, macht:
	Für jede Zahl i von 1 bis die Länge von text, mache:
		Speichere elm in text an der Stelle i.
Und kann so benutzt werden:
	"Fülle <text> mit <elm>"

[
	Gibt die Buchstaben im gegebenen Textes als Buchstaben Liste zurück.
		f("Hallo") -> ['H', 'a', 'l', 'l', 'l']
]
Die öffentliche Funktion Buchstaben_TextRef_BuchstabenListe mit dem Parameter text vom Typ Text Referenz, gibt eine Buchstaben Liste zurück, macht:
	Die Buchstaben Liste l ist die Länge von text Mal 0 als Buchstabe.
	Die Zahl i ist 1.
	Für jeden Buchstaben b in text, mache:
		Speichere b in l an der Stelle i.
		Erhöhe i um 1.
	Gib l zurück.
Und kann so benutzt werden:
	"die Buchstaben in <text>"

[
	Gibt die Buchstaben im gegebenen Textes als Buchstaben Liste zurück.
		f("Hallo") -> ['H', 'a', 'l', 'l', 'l']
]
Die öffentliche Funktion Buchstaben_Text_BuchstabenListe mit dem Parameter text vom Typ Text, gibt eine Buchstaben Liste zurück, macht:
	Gib die Buchstaben in text zurück.
Und kann so benutzt werden:
	"die Buchstaben in <text>"

[
	Gibt die Buchstaben im gegebenen Textes als Text Liste zurück.
		f("Hallo") -> ["H", "a", "l", "l", "o"]
]
Die öffentliche Funktion Buchstaben_TextRef_TextListe mit dem Parameter text vom Typ Text Referenz, gibt eine Text Liste zurück, macht:
	Die Text Liste l ist die Länge von text Mal "".
	Die Zahl i ist 1.
	Für jeden Buchstaben b in text, mache:
		Speichere b als Text in l an der Stelle i.
		Erhöhe i um 1.
	Gib l zurück.
Und kann so benutzt werden:
	"die Buchstaben in <text> als Text Liste"

[
	Gibt die Buchstaben im gegebenen Textes als Text Liste zurück.
		f("Hallo") -> ["H", "a", "l", "l", "o"]
]
Die öffentliche Funktion Buchstaben_Text_TextListe mit dem Parameter text vom Typ Text, gibt eine Text Liste zurück, macht:
	Gib die Buchstaben in text als Text Liste zurück.
Und kann so benutzt werden:
	"die Buchstaben in <text> als Text Liste"

[
	Gibt den ersten index des gegebenen Buchstaben im Text zurück oder -1 falls es nicht gefunden wurde. 
]
Die öffentliche Funktion Text_Index_Von_Buchstabe_Ref mit den Parametern text und elm vom Typ Text Referenz und Buchstabe, gibt eine Zahl zurück, macht:
	Wenn die Länge von text gleich 0 ist, gib -1 zurück.
	Die Zahl i ist 1.
	Für jeden Buchstaben b in text, mache:
		Wenn b gleich elm ist, gib i zurück.
		Erhöhe i um 1.
	Gib -1 zurück.
Und kann so benutzt werden:
	"der Index von <elm> in <text>" oder
	"den Index von <elm> in <text>"

[
	Gibt den ersten index des gegebenen Buchstaben im Text zurück oder -1 falls es nicht gefunden wurde. 
]
Die öffentliche Funktion Text_Index_Von_Buchstabe mit den Parametern text und elm vom Typ Text und Buchstabe, gibt eine Zahl zurück, macht:
	Gib den Index von elm in text zurück.
Und kann so benutzt werden:
	"der Index von <elm> in <text>" oder
	"den Index von <elm> in <text>"

[
	Gibt den ersten index des gegebenen Text im Text zurück oder -1 falls es nicht gefunden wurde. 
]
Die öffentliche Funktion Text_Index_Von_Text mit den Parametern text und elm vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Die Zahl n ist die Länge von elm.
	Wenn die Länge von text gleich 0 ist, gib -1 zurück.
	Wenn n gleich 0 ist, gib 1 zurück.
	Wenn n gleich 1 ist, gib den Index von (elm an der Stelle 1) in text zurück.
	Wenn n gleich die Länge von text ist, dann:
		Wenn elm gleich text ist, gib 1 zurück.
		Sonst gib -1 zurück. 
	Wenn n größer als die Länge von text ist, gib -1 zurück.

	Der Buchstabe c1 ist elm an der Stelle 1.
	Der Buchstabe c2 ist elm an der Stelle 2.
	Die Zahl i ist 1.
	Die Zahl t ist die Länge von text minus n plus 1.

	Solange i kleiner als, oder t ist, mache:
		Wenn text an der Stelle i ungleich c1 ist, dann:
			Die Zahl o ist der Index von c1 in (text im Bereich von (i plus 1) bis (i plus t)).
			Wenn o kleiner als 0 ist, gib -1 zurück.
			Erhöhe i um o.

		Wenn text an der Stelle (i plus 1) gleich c2 ist und text im Bereich von i bis (i plus n minus 1) gleich elm ist, gib i zurück.
		Erhöhe i um 1.
	Gib -1 zurück.
Und kann so benutzt werden:
	"der Index von <elm> in <text>" oder
	"den Index von <elm> in <text>"

[
	Gibt ob der gegebene Text leer ist zurück
]
Die öffentliche Funktion Ist_Text_Leer_Ref mit dem Parameter text vom Typ Text Referenz, gibt einen Wahrheitswert zurück, macht:
	Gib wahr, wenn die Länge von text gleich 0 ist, zurück.
Und kann so benutzt werden:
	"<text> <!nicht> leer ist"

[
	Gibt ob der gegebene Text leer ist zurück
]
Die öffentliche Funktion Ist_Text_Leer mit dem Parameter text vom Typ Text, gibt einen Wahrheitswert zurück, macht:
	Gib wahr, wenn text leer ist, zurück.
Und kann so benutzt werden:
	"<text> <!nicht> leer ist"

[
	Gibt zurück ob ein Text in eine Zahl umgewandelt werden kann
]
Die	öffentliche Funktion Text_Ist_Zahl_Ref mit dem Parameter t vom Typ Text Referenz, gibt einen Wahrheitswert zurück, macht:
	Die Zahl l ist die Länge von t.
	Wenn l kleiner als 1 ist, gib falsch zurück.

	Der Buchstabe Vorzeichen ist t an der Stelle 1.
	Wenn Vorzeichen eine Ziffer ist, gib wahr zurück.
	Wenn Vorzeichen ungleich '+' ist und Vorzeichen ungleich '-' ist, gib falsch zurück.
	Wenn l kleiner als 2 ist oder (t an der Stelle 2) keine Ziffer ist, gib falsch zurück.
	Gib wahr zurück.
Und kann so benutzt werden:
	"<t> <!nicht> in eine Zahl umgewandelt werden kann"

[
	Gibt zurück ob ein Text in eine Zahl umgewandelt werden kann
]
Die öffentliche Funktion Text_Ist_Zahl mit dem Parameter t vom Typ Text, gibt einen Wahrheitswert zurück, macht:
	Gib wahr, wenn t in eine Zahl umgewandelt werden kann, zurück.
Und kann so benutzt werden:
	"<t> <!nicht> in eine Zahl umgewandelt werden kann"

[
	Wandelt jeden Buchstaben des gegebenen Textes in die groß geschriebene Variante
]
Die öffentliche Funktion Großschreiben_Wert mit dem Parameter text vom Typ Text, gibt einen Text zurück, macht:
	Der Text neuerText ist "".
	Für jeden Buchstaben b in text, mache:
		Füge (b als großer Buchstabe) an neuerText an.
	Gib neuerText zurück.
Und kann so benutzt werden:
	"<text> groß geschrieben"

[
	Wandelt jeden Buchstaben des gegebenen Textes in die groß geschriebene Variante
]
Die öffentliche Funktion Großschreiben mit dem Parameter text vom Typ Text Referenz, gibt nichts zurück, macht:
	Speichere text groß geschrieben in text.
Und kann so benutzt werden:
	"Schreibe <text> groß"

[
	Wandelt jeden Buchstaben des gegebenen Textes in die klein geschriebene Variante
]
Die öffentliche Funktion Kleinschreiben_Wert mit dem Parameter text vom Typ Text, gibt einen Text zurück, macht:
	Der Text neuerText ist "".
	Für jeden Buchstaben b in text, mache:
		Füge (b als kleiner Buchstabe) an neuerText an.
	Gib neuerText zurück.
Und kann so benutzt werden:
	"<text> klein geschrieben"

[
	Wandelt jeden Buchstaben des gegebenen Textes in die klein geschriebene Variante
]
Die öffentliche Funktion Kleinschreiben mit dem Parameter text vom Typ Text Referenz, gibt nichts zurück, macht:
	Speichere text klein geschrieben in text.
Und kann so benutzt werden:
	"Schreibe <text> klein"

[
	z.B.:
	f("hallo", ' ', 8) -> "   hallo"
	f("hey", ' ', 8) -> "     hey"
	f("programm", ' ', 8) -> "programm"
	f("", 'o', 8) -> "oooooooo" 
]
Die öffentliche Funktion Polster_Links mit den Parametern text, zeichen und endlänge vom Typ Text, Buchstabe und Zahl, gibt einen Text zurück, macht:
	Die Zahl länge ist die Länge von text.
	Die Zahl gesuchteLänge ist endlänge minus länge.
	Wenn gesuchteLänge kleiner als, oder 0 ist, dann:
		Gib text zurück.
	
	Wiederhole:
		Stelle zeichen vor text.
	gesuchteLänge Mal.

	Gib text zurück.
Und kann so benutzt werden:
	"<text> mit <endlänge> <zeichen> links gepolstert"

[
	z.B.:
	f("hallo", ' ', 8) -> "hallo   "
	f("hey", ' ', 8) -> "hey     "
	f("programm", ' ', 8) -> "programm"
	f("", 'o', 8) -> "oooooooo" 
]
Die öffentliche Funktion Polster_Rechts mit den Parametern text, zeichen und endlänge vom Typ Text, Buchstabe und Zahl, gibt einen Text zurück, macht:
	Die Zahl länge ist die Länge von text.
	Die Zahl gesuchteLänge ist endlänge minus länge.
	Wenn gesuchteLänge kleiner als, oder 0 ist, dann:
		Gib text zurück.
	
	Wiederhole:
		Füge zeichen an text an.
	gesuchteLänge Mal.
	
	Gib text zurück.
Und kann so benutzt werden:
	"<text> mit <endlänge> <zeichen> rechts gepolstert"

[
	Spaltet den gegebenen Text anhand des angegebenen Buchstaben in Teiltexte. 
]
Die öffentliche Funktion Spalte mit den Parametern text und zeichen vom Typ Text und Buchstabe, gibt eine Text Liste zurück, macht:
	Wenn die Länge von text gleich 0 ist, gib eine leere Text Liste zurück.
	Die Text Liste endliste ist eine leere Text Liste.

	Die Zahl n ist die Anzahl der zeichen Buchstaben in text plus 1.

	Die Zahl i ist 1.
	Solange i kleiner als, oder n ist, mache:
		Die Zahl endIndex ist den Index von zeichen in text.
		Wenn endIndex kleiner als 0 ist, verlasse die Schleife.
        Wenn aber endIndex gleich 1 ist, speichere endliste verkettet mit "" in endliste.
        Sonst speichere endliste verkettet mit (text bis zum (endIndex minus 1). Element) in endliste.
		
        Wenn die Länge von text gleich 1 ist, Speichere "" in text.
        Sonst speichere text ab dem (endIndex plus 1). Element in text.

		Erhöhe i um 1.
	Speichere endliste verkettet mit text in endliste.
	Gib endliste bis zum i. Element zurück.
Und kann so benutzt werden:
	"<text> an <zeichen> gespalten"

[
	Spaltet den gegebenen Text anhand des angegebenen Textes in Teiltexte. 
]
Die öffentliche Funktion Spalte_Text mit den Parametern text und trenntext vom Typ Text und Text, gibt eine Text Liste zurück, macht:
	Die Zahl l ist die Länge von trenntext.
	Die Text Liste endliste ist eine leere Text Liste.
	Wenn l gleich 0 ist, gib die Buchstaben in text als Text Liste zurück.
	Wenn l gleich 1 ist, gib text an (trenntext an der Stelle 1) gespalten zurück.

	Die Zahl n ist die Anzahl der nicht überlappenden Subtexte trenntext in text plus 1.

	Die Zahl i ist 1.
	Solange i kleiner als, oder n ist, mache:
		Die Zahl endIndex ist den Index von trenntext in text.
		Wenn endIndex kleiner als 0 ist, verlasse die Schleife.
        Wenn aber endIndex gleich 1 ist, speichere endliste verkettet mit "" in endliste.
        Sonst speichere endliste verkettet mit (text bis zum (endIndex minus 1). Element) in endliste.
		
        Wenn die Länge von text gleich l ist, Speichere "" in text.
        Sonst speichere text ab dem (endIndex plus l). Element in text.

		Erhöhe i um 1.

	Speichere endliste verkettet mit text in endliste.
	Gib endliste bis zum i. Element zurück.
Und kann so benutzt werden:
	"<text> an <trenntext> gespalten"

[
	Gibt alle Indizes des gegebenen Subtextes im Text zurück.
]
Die öffentliche Funktion Finde_Subtext mit den Parametern text und subtext vom Typ Text und Text, gibt eine Zahlen Liste zurück, macht:
	Die Zahl lt ist die Länge von text.
	Die Zahl ls ist die Länge von subtext.
	Die Zahlen Liste l ist eine leere Zahlen Liste.
	Wenn ls gleich 0 ist oder lt gleich 0 ist oder ls größer als lt ist, gib l zurück.
	Wenn lt gleich ls ist, gib eine Liste, die aus 1 besteht zurück.

	Die Zahl start ist 1.
	Solange start kleiner als lt ist, mache:
		Die Zahl i ist den Index von subtext in (text ab dem start. Element).
		Wenn i gleich -1 ist, verlasse die Schleife.
		
		Erhöhe i um start minus 1.
		Speichere l verkettet mit i in l.
		Speichere i plus ls in start.
	Gib l zurück.
Und kann so benutzt werden:
	"alle Indizes vom Subtext <subtext> in <text>"

[
	Verkettet alle Elemente der Liste mit dem Trennzeichen und gibt den Text zurück.
	z.B.:
		f(["hi", "", "yo"], '-') -> "hi--yo"
]
Die öffentliche Funktion Verbinden_Text mit den Parametern liste und trennzeichen vom Typ Text Liste und Buchstabe, gibt einen Text zurück, macht:
	Der Text ret ist ein leerer Text.
	Für jede Zahl i von 1 bis die Länge von liste, mache:
		Wenn i kleiner als die Länge von liste ist, Speichere ret verkettet mit liste an der Stelle i verkettet mit trennzeichen in ret.
		Sonst Speichere ret verkettet mit liste an der Stelle i in ret.
	Gib ret zurück.
Und kann so benutzt werden:
	"<liste> mit dem Trennzeichen <trennzeichen> zum Text verbunden"

[
	Verkettet alle Elemente der Liste mit dem Trennzeichen und gibt den Text zurück.
	z.B.:
		f([1, 234, 56789, 0], '-') -> "1-234-56789-0"
]
Die öffentliche Funktion Verbinden_Zahl mit den Parametern liste und trennzeichen vom Typ Zahlen Liste und Buchstabe, gibt einen Text zurück, macht:
	Der Text ret ist ein leerer Text.
	Für jede Zahl i von 1 bis die Länge von liste, mache:
		Wenn i kleiner als die Länge von liste ist, Speichere ret verkettet mit (liste an der Stelle i) als Text verkettet mit trennzeichen in ret.
		Sonst Speichere ret verkettet mit (liste an der Stelle i) als Text in ret.
	Gib ret zurück.
Und kann so benutzt werden:
	"<liste> mit dem Trennzeichen <trennzeichen> zum Text verbunden"

[
	Verkettet alle Elemente der Liste mit dem Trennzeichen und gibt den Text zurück.
	z.B.:
		f([1,4, 0 durch 0, 23,0], '-') -> "1,4-nan-23"
]
Die öffentliche Funktion Verbinden_Kommazahl mit den Parametern liste und trennzeichen vom Typ Kommazahlen Liste und Buchstabe, gibt einen Text zurück, macht:
	Der Text ret ist ein leerer Text.
	Für jede Zahl i von 1 bis die Länge von liste, mache:
		Wenn i kleiner als die Länge von liste ist, Speichere ret verkettet mit (liste an der Stelle i) als Text verkettet mit trennzeichen in ret.
		Sonst Speichere ret verkettet mit (liste an der Stelle i) als Text in ret.
	Gib ret zurück.
Und kann so benutzt werden:
	"<liste> mit dem Trennzeichen <trennzeichen> zum Text verbunden"

[
	Verkettet alle Elemente der Liste mit dem Trennzeichen und gibt den Text zurück.
	z.B.:
		f(['a', 'b', 'c'], '-') -> "a-b-c"
]
Die öffentliche Funktion Verbinden_Buchstabe mit den Parametern liste und trennzeichen vom Typ Buchstaben Liste und Buchstabe, gibt einen Text zurück, macht:
	Der Text ret ist ein leerer Text.
	Für jede Zahl i von 1 bis die Länge von liste, mache:
		Wenn i kleiner als die Länge von liste ist, Speichere ret verkettet mit (liste an der Stelle i) verkettet mit trennzeichen in ret.
		Sonst Speichere ret verkettet mit (liste an der Stelle i) in ret.
	Gib ret zurück.
Und kann so benutzt werden:
	"<liste> mit dem Trennzeichen <trennzeichen> zum Text verbunden"

[
	Verkettet alle Elemente der Liste mit dem Trennzeichen und gibt den Text zurück.
	z.B.:
		f([wahr, falsch, falsch], '-') -> "wahr-falsch-falsch"
]
Die öffentliche Funktion Verbinden_Wahrheitswert mit den Parametern liste und trennzeichen vom Typ Wahrheitswert Liste und Buchstabe, gibt einen Text zurück, macht:
	Der Text ret ist ein leerer Text.
	Für jede Zahl i von 1 bis die Länge von liste, mache:
		Wenn i kleiner als die Länge von liste ist, Speichere ret verkettet mit (liste an der Stelle i) als Text verkettet mit trennzeichen in ret.
		Sonst Speichere ret verkettet mit (liste an der Stelle i) als Text in ret.
	Gib ret zurück.
Und kann so benutzt werden:
	"<liste> mit dem Trennzeichen <trennzeichen> zum Text verbunden"

[
	Berechnet die Hamming_Distanz zwischen t1 und t2:
		"karolin" und "kathrin" ist 3.
		"karolin" und "kerstin" ist 3.
		"kathrin" und "kerstin" ist 4

	Wenn die Länge der beiden Texte ungleich ist, wird -1 zurückgegeben:
		"karolin" und "karoli" ist -1.
]
Die öffentliche Funktion Hamming_Distanz mit den Parametern t1 und t2 vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Wenn die Länge von t1 ungleich die Länge von t2 ist, gib -1 zurück.
	Die Zahl summe ist 0.
	Für jede Zahl i von 1 bis die Länge von t1, mache:
		Wenn t1 an der Stelle i ungleich t2 an der Stelle i ist, erhöhe summe um 1.
	Gib summe zurück.
Und kann so benutzt werden:
	"die Zahl der Änderungen benötigt um <t1> in <t2> umzuwandeln" oder
	"die Hamming-Distanz zwischen <t1> und <t2>"

Binde Min3 aus "Duden/Mathe" ein.
[
	Berechnet die Levenshtein Distanz zwischen zwei Texten:
		"Bar" und "Bar" ist 0
		"Bar" und "Bir" ist 1
		"Bar" und "Bier" ist 2
		"Bar" und "Ba" ist 1
		"kitten" und "sitting" ist 3
		"Google" und "Facebook" ist 8
		"Die Deutsche Programmiersprache" und "The German Programming language" ist 19
]
Die öffentliche Funktion Levenshtein_Distanz mit den Parametern t1 und t2 vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Die Zahlen Liste cost ist die Länge von t2 plus 1 Mal 0.
	Die Zahlen Liste newCost ist die Länge von t2 plus 1 Mal 0.

	Für jede Zahl i von 0 bis die Länge von t2, mache:
		Speichere i in cost an der Stelle (i plus 1).

	Für jede Zahl i von 1 bis die Länge von t1, mache:
		Speichere i in newCost an der Stelle 1.
	
		Für jede Zahl j von 1 bis die Länge von t2, mache:
			Die Zahl deletionCost ist cost an der Stelle (j plus 1) plus 1.
			Die Zahl insertionCost ist newCost an der Stelle j plus 1.
			Die Zahl substitutionCost ist cost an der Stelle j.

			Wenn t1 an der Stelle i ungleich t2 an der Stelle j ist, erhöhe substitutionCost um 1.
			
			Speichere die kleinere Zahl von deletionCost, insertionCost und substitutionCost in newCost an der Stelle (j plus 1).
		Die Zahlen Liste temp ist cost.
		Speichere newCost in cost.
		Speichere temp in newCost.
	Gib cost an der Stelle (die Länge von t2 plus 1) zurück.
Und kann so benutzt werden:
	"wie ähnlich <t1> und <t2> sind" oder
	"die Levenshtein-Distanz zwischen <t1> und <t2>"

[
	Gibt 0 zurück wenn text1 gleich text2 ist.
	Gibt >0 zurück wenn der erste ungleiche Buchstabe in text1 größer als text2 ist
	Gibt <0 zurück wenn der erste ungleiche Buchstabe in text1 kleiner als text2 ist
	
	Wenn zwei Texte unterschiedlicher länge aber mit selben Präfix verglichen werden (z.B.: Haallö und Haallöchen)
	gibt die Funktion -1 wenn text2 und 1 wenn text1 länger ist zurück. 
]
Die öffentliche Funktion Vergleiche_Text mit den Parametern text1 und text2 vom Typ Text und Text, gibt eine Zahl zurück, macht:
	Wenn text1 gleich text2 ist, gib 0 zurück.

	Die Zahl i ist 1.
	Solange text1 an der Stelle i gleich text2 an der Stelle i ist, mache:
		Erhöhe i um 1.
		Wenn i größer als die Länge von text1 ist, gib -1 zurück.
		Wenn i größer als die Länge von text2 ist, gib 1 zurück.
		
	Gib (text1 an der Stelle i) als Zahl minus (text2 an der Stelle i) als Zahl zurück.
Und kann so benutzt werden:
	"<text1> mit <text2> verglichen"

[
	Spaltet text anhand der gegebenen Spaltmenge in Teiltexte.
	Beispiel: text = "Hallo\n\rWelt\n!", spaltmenge = "\n\r" -> ["Hallo", "Welt", "!"]
]
Die öffentliche Funktion Spalten_Spaltmenge_Text_Ref mit den Parametern text und spaltmenge vom Typ Text Referenz und Buchstaben Listen Referenz, gibt eine Text Liste zurück, macht:
	Wenn die Länge von text gleich 0 ist, gib eine leere Text Liste zurück.
	Wenn die Länge von spaltmenge gleich 0 ist, gib text als Text Liste zurück.

	Die Text Liste endliste ist eine leere Text Liste.
	
	Der TextIterator iter ist ein TextIterator über text.
	Solange iter nicht zuende ist, mache:
		Solange spaltmenge (den momentanen Buchstaben von iter) enthält, erhöhe iter um 1.
		Wenn iter zuende ist, verlasse die Schleife.

		Die Zahl i ist der momentane Index von iter.
		Solange iter nicht zuende ist und spaltmenge (den momentanen Buchstaben von iter) nicht enthält, mache:
			Erhöhe iter um 1.
		Füge (text im Bereich von i bis (iter als Zahl minus 1)) an endliste an.
		Erhöhe iter um 1.

	Gib endliste zurück.
Und kann so benutzt werden:
	"<text> anhand der Spaltmenge <spaltmenge> gespalten"

[
	Spaltet text anhand der gegebenen Spaltmenge in Teiltexte.
	Beispiel: text = "Hallo\n\rWelt\n!", spaltmenge = "\n\r" -> ["Hallo", "Welt", "!"]
]
Die öffentliche Funktion Spalten_Spaltmenge_Text mit den Parametern text und spaltmenge vom Typ Text und Buchstaben Liste, gibt eine Text Liste zurück, macht:
	Gib text anhand der Spaltmenge spaltmenge gespalten zurück.
Und kann so benutzt werden:
	"<text> anhand der Spaltmenge <spaltmenge> gespalten"

[
	Spaltet text anhand der gegebenen Spaltmenge in Teiltexte.
	Beispiel: text = "Hallo\n\rWelt\n!", spaltmenge = "\n\r" -> ["Hallo", "Welt", "!"]
]
Die öffentliche Funktion Spalten_Spaltmenge_Text_RefMenge mit den Parametern text und spaltmenge vom Typ Text und Buchstaben Listen Referenz, gibt eine Text Liste zurück, macht:
	Gib text anhand der Spaltmenge spaltmenge gespalten zurück.
Und kann so benutzt werden:
	"<text> anhand der Spaltmenge <spaltmenge> gespalten"

[
	Spaltet text anhand der gegebenen Spaltmenge in Teiltexte.
	Beispiel: text = "Hallo\n\rWelt\n!", spaltmenge = "\n\r" -> ["Hallo", "Welt", "!"]
]
Die öffentliche Funktion Spalten_SpaltmengeText_Text mit den Parametern text und spaltmenge vom Typ Text und Text, gibt eine Text Liste zurück, macht:
	Gib text anhand der Spaltmenge (die Buchstaben in spaltmenge) gespalten zurück.
Und kann so benutzt werden:
	"<text> anhand der Spaltmenge <spaltmenge> gespalten"

Die Buchstaben Liste leerzeichen ist eine Liste, die aus 
' ', '\n', '\t', '\r', 13 als Buchstabe, 14 als Buchstabe
besteht.

[
	Gibt die von leerzeichen getrennten Worte in text zurück.
	Leerzeichen: ' ', '\n', '\t', '\r', 13 als Buchstabe, 14 als Buchstabe.
	Beispiel:
		"Die Worte in Hallo Welt!" -> ["Die", "Worte", "in", "Hallo", "Welt!"]
		"Die\r\nWorte in \rHallo Welt!" -> ["Die", "Worte", "in", "Hallo", "Welt!"]
]
Die öffentliche Funktion Text_Worte_Ref mit dem Parameter text vom Typ Text Referenz, gibt eine Text Liste zurück, macht:
	Gib text anhand der Spaltmenge leerzeichen gespalten zurück.
Und kann so benutzt werden:
	"die Worte in <text>",
	"den Worten in <text>",
	"alle Worte in <text>",
	"allen Worten in <text>",
	"<text> in Worte unterteilt"

Die öffentliche Funktion Text_Worte mit dem Parameter text vom Typ Text, gibt eine Text Liste zurück, macht:
	Gib die Worte in text zurück.
Und kann so benutzt werden:
	"die Worte in <text>",
	"den Worten in <text>",
	"alle Worte in <text>",
	"allen Worten in <text>",
	"<text> in Worte unterteilt"

Die öffentliche Funktion Text_Zu_ByteListe mit dem Parameter t vom Typ Text Referenz, gibt eine Byte Liste zurück,
ist in "libddpstdlib.a" definiert
und kann so benutzt werden:
	"die Bytes von <t>" oder
	"den Bytes von <t>" oder
	"der Bytes von <t>"

Die öffentliche Funktion Text_Zu_ByteListe_Wert mit dem Parameter t vom Typ Text, gibt eine Byte Liste zurück, macht:
	Gib die Bytes von t zurück.
Und kann so benutzt werden:
	"die Bytes von <t>" oder
	"den Bytes von <t>" oder
	"der Bytes von <t>"

Die öffentliche Funktion ByteListe_Zu_Text mit dem Parameter b vom Typ Byte Listen Referenz, gibt einen Text zurück,
ist in "libddpstdlib.a" definiert
und kann so benutzt werden:
	"die Bytes <b> als Text" oder
	"den Bytes <b> als Text" oder
	"der Bytes <b> als Text"

Die öffentliche Funktion ByteListe_Zu_Text_Wert mit dem Parameter b vom Typ Byte Liste, gibt einen Text zurück, macht:
	Gib die Bytes b als Text zurück.
Und kann so benutzt werden:
	"die Bytes <b> als Text" oder
	"den Bytes <b> als Text" oder
	"der Bytes <b> als Text"